apply plugin: 'java'
apply plugin: 'eclipse'
buildDir 'bin'
sourceCompatibility = 1.7
version = '0.5'

String stringify(Object o){
  if (o == null){
    return 'null'
  } 
  if (o.properties == null){
    return 'no groovy properties'
  }
  def kvs =  o.properties.collect{ k,v ->
     "${k} : ${v}"
  }
  def stringy=kvs.join('\n')
  println "STRING: ${stringy}"
  return stringy
}

void dumpObject(Object o){
  println "DUMP: ${stringify(o)}" 
}

task wrapper(type: Wrapper){
  gradleVersion = '2.4'
}

def batik_artifacts = [
	'batik-' : [ 
	'util', 
	'transcoder',
	'ext',
	'awt-util',
	'bridge',
	'dom',
	'svggen',
	'svg-dom',
	'gvt',
	'parser',
	'codec',
	'xml',
	'script',
	'anim',
	'js',
	'rasterizer',
	'swing',
	'extension',
	'gui-util',
	'ttf2svg',
	'svgpp' 
	],
	]

repositories {
	mavenCentral()
}
uploadArchives {
  repositories {
    flatDir {
      dirs 'dist'
    }
  }
}

dependencies {
	compile group: 'org.json', name: 'json', version: '20141113'
	compile group: 'xml-apis', name: 'xml-apis', version: '1.0.b2'
	compile group: 'org.apache.xmlgraphics', name: 'fop', version: '1.1'
	compile group: 'org.apache.avalon.framework', name: 'avalon-framework-api', version: '4.3.1'
	compile group: 'org.apache.avalon.framework', name: 'avalon-framework-impl', version: '4.3.1'
	compile group: 'com.google.guava', name: 'guava', version: '18.0'
	compile fileTree(dir: 'depends', include: '*.jar')
	batik_artifacts.each() { prefix, suffixes ->
		suffixes.each() { suffix ->
			def artname = "$prefix$suffix"
			//println "getting artifact $artname"
			compile group: 'org.apache.xmlgraphics', name: artname, version: '1.7'
		}
	}	
  testCompile group: 'junit', name: 'junit', version: '4.+'
}

sourceSets {
	main {
		java {
			//srcDir 'src'
		}
	}
}

test {
// listen to events in the test execution lifecycle
  beforeTest { descriptor ->
     logger.lifecycle("Running test: " + descriptor)
  }

  // listen to standard out and standard error of the test JVM(s)
  onOutput { descriptor, event ->
     logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
  }
}

jar {
	manifest {
		attributes( 
			'Implementation-Title': 'Orion Plot',
			'Implementation-Version': version,
			'Main-Class' : 'bio.comp.orion.OrionLauncher',
			'Class-Path' : configurations.runtime.collect { it.getName() }.join(' '))
	}
}

task fullJar(type: Copy, dependsOn:jar) {
	println "jar task $jar.archivePath $jar.destinationDir"
	from configurations.runtime
	into jar.destinationDir
}

String getJavaHome(){
  return "${System.getProperty('java.home')}"
}

String getJava(){
  return [ getJavaHome(), 'bin' , 'java' ].join(File.separator) 
}

task showJavaHome {
  description "Displays the java home directory used to execute this gradle build"
  doLast{
    println getJavaHome()
  }  
}

task runOrionGUI(type: JavaExec, dependsOn: jar) {
  description = 'Run OrionPlot GUI'
  classpath = sourceSets.main.runtimeClasspath

  main = 'bio.com.orion.OrionLauncher'

  // arguments to pass to the application
  //args 'appArg1'
  doFirst {
    println "running ${getJava()} -jar '${jar.archivePath}'"
  } 
} 
